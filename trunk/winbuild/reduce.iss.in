;;----------------------------------------------------------------;;
;; -- reduce.iss --  REDUCE InnoSetup installation script         ;;
;;----------------------------------------------------------------;;
;;                                                                ;;
;;  * InnoSetup version 5.1.7 or above is recommended             ;;
;;                                                                ;;
;;  * IMPORTANT!                                                  ;;
;;    Section [CustomMessages] below contains messages            ;;
;;    in several 8-bit encodings (code pages 1251, 1252).         ;;
;;    Don't use any encoding conversion tool (e.g. iconv,         ;;
;;    recode) on this file directly!                              ;;
;;                                                                ;;
;;----------------------------------------------------------------;;


;;#define CslBuild    "@here@\cslbuild"
;;#define PslBuild    "@srcdir@\pslbuild\@build@"
;;#define PslBuild64  "@srcdir@\pslbuild\@build64@"

[Setup]
AppName=REDUCE
AppVerName=REDUCE
AppId=REDUCE
UsePreviousAppDir=yes
AppPublisher=REDUCE Developers
VersionInfoCopyright=REDUCE Developers
AppPublisherURL=http://reduce-algebra.sourceforge.net/
AppSupportURL=http://reduce-algebra.sourceforge.net/
AppUpdatesURL=http://reduce-algebra.sourceforge.net/
AppVersion=@VERSION@
OutputBaseFilename=Reduce-Setup
DefaultDirName={pf}\Reduce
DefaultGroupName=Reduce
AllowNoIcons=yes
LicenseFile=@srcdir@\csl\reduce.doc\BINARY-LICENSE.txt
Compression=lzma/ultra
SolidCompression=yes
Uninstallable=yes
UninstallFilesDir={app}\uninst
ShowLanguageDialog=yes
ChangesAssociations=yes
PrivilegesRequired=none
ArchitecturesInstallIn64BitMode=x64


[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "de"; MessagesFile: "compiler:Languages\German.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"
Name: "it"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "es"; MessagesFile: "compiler:Languages\Spanish.isl"


[Types]
Name: "full"; Description: "{cm:FullInstallation}"
Name: "cslonly"; Description: "{cm:CslOnlyInstallation}"
Name: "pslonly"; Description: "{cm:PslOnlyInstallation}"
Name: "custom"; Description: "{cm:CustomInstallation}"; Flags: iscustom


[Components]
Name: "common"; Description: "{cm:ReduceCommon}"; Types: full cslonly pslonly custom; Flags: fixed
Name: "csl"; Description: "{cm:ReduceCSL}"; Types: full cslonly custom
Name: "psl"; Description: "{cm:ReducePSL}"; Types: full pslonly custom


[Files]
; common
Source: "@srcdir@\csl\reduce.doc\BINARY-LICENSE.txt"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\csl\reduce.doc\BSD-LICENSE.txt"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\csl\reduce.doc\LGPL-2.1.txt"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\misc\primer.tex"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\misc\primer.pdf"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\misc\sl.tex"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\misc\sl.bib"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\misc\sl.pdf"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\manual\redlogo.png"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\manual\*.tex"; DestDir: "{app}\doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\manual\manual.pdf"; DestDir: "{app}\lib\csl\reduce.doc\"; Flags: ignoreversion; Components: "common"
Source: "@srcdir@\doc\manual\manual*.html"; DestDir: "{app}\lib\csl\reduce.doc\"; Components: "common"
Source: "@srcdir@\doc\manual\manual*.png"; DestDir: "{app}\lib\csl\reduce.doc\"; Components: "common"
Source: "@srcdir@\doc\manual\manual.css"; DestDir: "{app}\lib\csl\reduce.doc\"; Components: "common"
Source: "@srcdir@\doc\manual\manual.html"; DestDir: "{app}\lib\csl\reduce.doc\"; DestName: "index.html"; Components: "common"
Source: "@srcdir@\winbuild\gp460-win32-setup.exe"; DestDir: "{app}\lib\"; Components: "common"

Source: "@srcdir@\packages\*.*"; Excludes: "regressions\*"; DestDir: "{app}\packages\"; Flags: recursesubdirs; Components: "common"

; csl
Source: "cslbuild\reduce.exe"; DestDir: "{app}\lib\csl\"; Components: "csl";
Source: "cslbuild\winreduce.exe"; DestDir: "{app}\lib\csl\"; Components: "csl";
Source: "cslbuild\reduce.img"; DestDir: "{app}\lib\csl\"; Components: "csl";
Source: "cslbuild\reduce.fonts\*.*"; DestDir: "{app}\lib\csl\reduce.fonts"; Components: "csl";
Source: "cslbuild\reduce.resources\*.*"; DestDir: "{app}\lib\csl\reduce.resources"; Components: "csl";

Source: "cslbuild\make-cygwin-symlink.exe"; DestDir: "{app}\lib\csl\"; Components: "csl";

;; I think that the reduce.bat script is probably not much use here now.
;;Source: "@srcdir@\bin\redcsl.bat"; DestDir: "{app}\bin\"; Components: "csl"


;psl
Source: "@srcdir@\pslbuild\@build@\psl\*"; DestDir: "{app}\lib\psl\psl\"; Components: "psl"; Check: not Is64BitInstallMode
Source: "@srcdir@\pslbuild\@build@\red\*.b"; DestDir: "{app}\lib\psl\red\"; Components: "psl"; Check: not Is64BitInstallMode
Source: "@srcdir@\pslbuild\@build@\red\*.lap"; DestDir: "{app}\lib\psl\red\"; Components: "psl"; Flags: skipifsourcedoesntexist; Check: not Is64BitInstallMode

Source: "@srcdir@\pslbuild\@build64@\psl\*"; DestDir: "{app}\lib\psl\psl\"; Components: "psl"; Check: Is64BitInstallMode
Source: "@srcdir@\pslbuild\@build64@\red\*.b"; DestDir: "{app}\lib\psl\red\"; Components: "psl"; Check: Is64BitInstallMode
Source: "@srcdir@\pslbuild\@build64@\red\*.lap"; DestDir: "{app}\lib\psl\red\"; Components: "psl"; Check: Is64BitInstallMode; Flags: skipifsourcedoesntexist;

Source: "@srcdir@\winbuild\reduce.img"; DestDir: "{app}\lib\psl\red\"; Components: "psl"; Check: Is64BitInstallMode
Source: "@srcdir@\winbuild\reduce64.img"; DestDir: "{app}\lib\psl\red\"; DestName: "reduce.img"; Components: "psl"; Check: not Is64BitInstallMode
Source: "@srcdir@\winbuild\redpsl.bat"; DestDir: "{app}\bin\"; Components: "psl"
Source: "@srcdir@\winbuild\redpslw.bat"; DestDir: "{app}\bin\"; Components: "psl"


[Icons]
;Name: "{group}\redcsl"; Filename: "{app}\bin\redcsl.bat"; IconFileName: "{app}\bin\redcsl.bat"; Components: "csl"
Name: "{group}\CSL Reduce"; Filename: "{app}\lib\csl\winreduce.exe"; Components: "csl"
Name: "{group}\PSL Reduce"; Filename: "{app}\bin\redpsl.bat"; IconFileName: "{app}\lib\psl\psl\bpslw.exe"; Components: "psl"


[Run]
Filename: "{app}\lib\gp460-win32-setup.exe"; Description: "Install Gnuplot"; Flags: postinstall; Check: MyGnuPlotCheck
Filename: "{app}\lib\csl\make-cygwin-symlink.exe"; Description: "set symlinks for benefit of cygwin users"; Parameters: "{app}\lib\csl\reduce.exe"; Components: "csl"

[Code]
var
  NeedGnuPlotInstall: Boolean;
  GnuPlotPath: String;

function InitializeSetup(): Boolean;
var
  RegVal: String;
begin
  Log('InitializeSetup called');
  if not RegQueryStringValue(HKEY_CLASSES_ROOT, 'gnuplot\shell\open\command', '', RegVal) then begin
    NeedGnuPlotInstall := True;
    Log('No Gnuplot Installed');
  end else begin
    NeedGnuPlotInstall := False;
    GnuPlotPath := RegVal;
    Delete(GnuPlotPath, Pos(' -p ', RegVal), Length(RegVal));
    Log('Gnuplot found at '+GnuPlotPath);
  end;
  Result := True;
end;


function MyGnuPlotCheck(): Boolean;
begin
  Result := NeedGnuPlotInstall;
end;



[CustomMessages]


;=== English === LID=$0409 CodePage=0

FullInstallation=Full Installation
CslOnlyInstallation=Install CSL only
PslOnlyInstallation=Install PSL Only
CustomInstallation=Custom Installation

ReduceCommon=Files common to CSL and PSL
ReduceCSL=Files for CSL
ReducePSL=Files for PSL



;=== German === LID=$0407 CodePage=1252

de.FullInstallation=Vollständige Installation
de.CslOnlyInstallation=Nur CSL installieren
de.PslOnlyInstallation=Nur PSL installieren
de.CustomInstallation=Benutzerdefinierte Installation

de.ReduceCommon=Gemeinsame Dateien
de.ReduceCSL=CSL-Dateien
de.ReducePSL=PSL-Dateien


;=== French === LID=$040C CodePage=1252

fr.FullInstallation=Installation complète
fr.CslOnlyInstallation=Installez CSL seulement
fr.PslOnlyInstallation=Installez PSL seulement
fr.CustomInstallation=Installation personnalisée

fr.ReduceCommon=Fichiers communs à CSL et PSL
fr.ReduceCSL=Fichiers pour CSL
fr.ReducePSL=Fichiers pour PSL


; === Italian ===

it.FullInstallation=Installazione completa
it.CslOnlyInstallation=Installare solo CSL
it.PSLOnlyInstallation=Installare solo PSL
it.CustomInstallation=Installazione personalizzata

it.ReduceCommon=File comuni a CSL e PSL
it.ReduceCSL=File per CSL
it.ReducePSL=File per PSL


es.FullInstallation=Instalación completa
es.CslOnlyInstallation=Instalar sólo CSL
es.PSLOnlyInstallation=Instalar sólo PSL
es.CustomInstallation=Instalación personalizada

es.ReduceCommon=Archivos comunes de CSL y PSL
es.ReduceCSL=Archivos para CSL
es.ReducePSL=Archivos para PSL

